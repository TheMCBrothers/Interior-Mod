plugins {
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

group = 'net.themcbrothers'
archivesBaseName = 'TheMCBrothersInteriorMod'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }

    withJavadocJar()
    withSourcesJar()
}

minecraft {
    mappings channel: "$mappings_channel", version: "$mappings_version"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                interiormod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'interiormod'

            mods {
                interiormod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'interiormod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                interiormod {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url = "https://nexus.themcbrothers.net/repository/maven-blamejared" }
    maven { url = "https://nexus.themcbrothers.net/repository/maven-mod" }
    maven { url = "https://nexus.themcbrothers.net/repository/maven-k4you" }
    maven { url = "https://nexus.themcbrothers.net/repository/maven-curse" }
}

dependencies {
    // Minecraft Forge
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Configured
    if (project.hasProperty('configured_file_id')) {
        runtimeOnly fg.deobf("curse.maven:configured-457570:$project.configured_file_id")
    }

    // Just Enough Items
    if (project.hasProperty('jei_version')) {
        // compile against the JEI API but do not include it at runtime
        compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"))
        compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"))
        // at runtime, use the full JEI jar for Forge
        runtimeOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}"))
    }
}

jar {
    archiveAppendix.set("$project.minecraft_version")
    manifest {
        attributes([
                "Specification-Title"     : "interiormod",
                "Specification-Vendor"    : "TheMCBrothers",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "TheMCBrothers",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

javadoc {
    source = [sourceSets.main.allJava]
    failOnError = false
}

task deobfJar(type: Jar) {
    archiveAppendix.set("$project.minecraft_version")
    archiveClassifier.set('deobf')
    from sourceSets.main.output
}

sourcesJar {
    archiveAppendix.set("$project.minecraft_version")
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

javadocJar {
    archiveAppendix.set("$project.minecraft_version")
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

task apiJar(type: Jar) {
    archiveAppendix.set("$project.minecraft_version")
    archiveClassifier.set('api')
    include 'net/themcbrothers/interiormod/api/**'
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives javadocJar
    archives apiJar
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = "$project.minecraft_version-$project.version"
            artifactId = "$project.mavenname"
            artifact jar
        }
    }
    repositories {
        maven {
            credentials {
                username = getSecret('nexus_uname')
                password = getSecret('nexus_pwd')
            }

            url 'https://nexus.themcbrothers.net/repository/maven-releases/'
        }
    }
}

private String getChangelogText() {
    File changelogFile = file('changelog.txt')
    String str = ''
    int lineCount = 0
    boolean done = false
    changelogFile.eachLine {
        if (done || it == null) {
            return
        }
        if (it.length() > 1) {
            String temp = it
            if (lineCount == 0) {
                temp = "${modname} ${version}"
                temp = "<h2>$temp</h2>"
            } else if (it.startsWith('-')) {
                temp = "&nbsp;&nbsp;&nbsp;$temp"
                temp = temp.replaceAll("(\\S+/\\S+)#(\\d+)\\b", "<a href=\"https://github.com/\$1/issues/\$2\">\$0</a>")
                temp = temp.replaceAll("#(\\d+)\\b(?!</a>)", "<a href=\"https://github.com/$github_project/issues/\$1\">\$0</a>")
            } else {
                temp = "<h4>$temp</h4>"
            }
            str += "$temp<br/>"
            lineCount++
            return
        } else {
            done = true
        }
    }
    return str
}

private String getSecret(String key) {
    def secret = System.getenv(key)
    if (secret != null) {
        return secret.trim()
    }

    File secretFile = file('secrets.properties')
    if (secretFile.canRead()) {
        Properties secretProps = new Properties()
        secretProps.load(new FileInputStream(secretFile))
        return secretProps.getProperty(key).toString()
    }
    return null
}

curseforge {
    apiKey = getSecret('cf_key')

    project {
        id = project.cf_project
        changelog = getChangelogText()
        changelogType = 'html'
        releaseType = project.artifact_type
        addGameVersion(project.minecraft_version)
        mainArtifact(jar) {
            displayName = "${version} for ${minecraft_version}"
            relations {
                optionalDependency 'jei'
            }
        }
        // addArtifact apiJar
    }
}

modrinth {
    token = getSecret('modrinth_token')
    projectId = project.modrinth_project
    versionNumber = "$project.minecraft_version-$project.version"
    versionName = project.version
    versionType = project.artifact_type
    changelog = getChangelogText()
    uploadFile = jar
    gameVersions = ["$project.minecraft_version"]
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
